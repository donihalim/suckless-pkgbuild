# Maintainer: DONIX <donihalim@protonmail.com>

_pkgbase=slock
pkgname=slock-dom
pkgver=1.4
pkgrel=1
pkgdesc='Simple X display locker'
url='http://tools.suckless.org/slock'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxrandr')
provides=(slock)
conflicts=(slock)
source=(
    https://dl.suckless.org/tools/slock-${pkgver}.tar.gz
    https://tools.suckless.org/slock/patches/dwmlogo/slock-dwmlogo-20210324.diff
    config.h
)

sha256sums=(
    'b53849dbc60109a987d7a49b8da197305c29307fd74c12dc18af0d3044392e6a'
    'a385cd5aac0b3885b7d174d2e3e921e847979be170546196049a812fa9c6e550'
    'SKIP')

prepare() {
	cd "${srcdir}/${_pkgbase}-${pkgver}"
    cp "${srcdir}/config.h" config.h # User defined config

    # Change nogroup to nobody, since it isn't exist on Arch Linux
	sed '/group =/s/nogroup/nobody/' -i config.def.h

    # This patch draws the dwm logo which changes color based on the state.
    # https://tools.suckless.org/slock/patches/dwmlogo/
    patch -p1 < ../slock-dwmlogo-20210324.diff
}

build() {
	cd "${srcdir}/${_pkgbase}-${pkgver}"
	NAME="${pkgname}" make
}

package() {
	cd "${srcdir}/${_pkgbase}-${pkgver}"
	NAME="{pkgname}" make PREFIX=/usr DESTDIR="${pkgdir}" install
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

